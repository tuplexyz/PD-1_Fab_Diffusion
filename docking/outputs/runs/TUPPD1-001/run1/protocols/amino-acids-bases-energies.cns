! amino-acids-bases-energies.cns
!    Calculate a free-energy of protein-nucleic acids interaction based
!    on the single amino-acids - bases free energies of Zagrovic NAR2015
!
! ***********************************************************************
! * Copyright 2003-2018 Alexandre Bonvin, Utrecht University.           *
! * All rights reserved.                                                *
! * This code is part of the HADDOCK software and governed by its       *
! * license. Please see the LICENSE file that should have been included *
! * as part of this package.                                            *
! ***********************************************************************
!
eval ($interactene_water=0.0)
eval ($interactene_methanol=0.0)
eval ($numcontacts=0)
eval($testcutoff=5.0)

evaluate ($icount = 1)
do (store4 = 0) (all)

! First find all residues making contacts within the testcutoff
!
eval($nchain1 = 0)
while ($nchain1 < $data.ncomponents) loop nloop1
  evaluate($nchain1 = $nchain1 + 1)
  do (store4 = 1) ( byres ( (segid $Toppar.prot_segid_$nchain1) and (not (segid $Toppar.prot_segid_$nchain1 or resn WAT or resn HOH or resn TIP* or resn DMS*) ) around $testcutoff ))
end loop nloop1

! initialize the contact energies to 0
!
eval($nchain1 = 0)
while ($nchain1 < $data.ncomponents) loop nloop1
  evaluate($nchain1 = $nchain1 + 1)
  eval($nchain2 = 0)
  while ($nchain2 < $data.ncomponents) loop nloop2
    evaluate ($nchain2 = $nchain2 + 1)
    if ($nchain1 ne $nchain2) then
      for $at1_id in id ( tag and segid $Toppar.prot_segid_$nchain1 ) loop atm1
        show (resn) (id $at1_id)
        evaluate ($rsn1 = $result)
        for $at2_id in id ( tag and segid $Toppar.prot_segid_$nchain2 ) loop atm2
           show (resn) (id $at2_id)
           evaluate ($rsn2 = $result)
           evaluate ($dbwatene.$rsn1_$rsn2 = 0.0)
           evaluate ($dbwatene.$rsn2_$rsn1 = 0.0)
           evaluate ($dbmethene.$rsn1_$rsn2 = 0.0)
           evaluate ($dbmethene.$rsn2_$rsn1 = 0.0)
        end loop atm2
      end loop atm1
    end if
  end loop nloop2
end loop nloop1

! read in the contact energy data
!
inline @RUN:protocols/dbene-methanol.dat
inline @RUN:protocols/dbene-water.dat

! new calculate the contact energy for all contacts
!
eval($nchain1 = 0)
while ($nchain1 < $data.ncomponents) loop nloop1
  evaluate($nchain1 = $nchain1 + 1)
  eval($nchain2 = $nchain1)
  while ($nchain2 < $data.ncomponents) loop nloop2
    evaluate ($nchain2 = $nchain2 + 1)
    for $at1_id in id ( tag and segid $Toppar.prot_segid_$nchain1 and attr store4 = 1) loop atm1
        show (resn) (id $at1_id)
        evaluate ($rsn1 = $result)
        for $at2_id in id ( tag and segid $Toppar.prot_segid_$nchain2 and attr store4 = 1 ) loop atm2
           show (resn) (id $at2_id)
           evaluate ($rsn2 = $result)
!
! check for distance cutoff
!
           do (store8 = 0) (all)
           do (store8 = 1) (byres (id $at1_id) and (byres (id $at2_id)) around $testcutoff)
           show sum (store8) (all)
           if ($result > 0) then
             evaluate ($numcontacts = $numcontacts + 1)
             evaluate ($interactene_water = $interactene_water + $dbwatene.$rsn1_$rsn2)
             evaluate ($interactene_water = $interactene_water + $dbwatene.$rsn2_$rsn1)
             evaluate ($interactene_methanol = $interactene_methanol + $dbmethene.$rsn1_$rsn2)
             evaluate ($interactene_methanol = $interactene_methanol + $dbmethene.$rsn2_$rsn1)
           end if

        end loop atm2
      end loop atm1
  end loop nloop2
end loop nloop1

!display $numcontacts "contacts identified"
!display "interaction free-energy in water = " $interactene_water
!display "interaction free-energy in methanol = " $interactene_methanol

