module (ncomponents; Toppar;)
    {Create and initialize Dummy residues for each segid

    Parameters
    ----------
    ncomponents : int
        Number of components/segids

    Toppar : data-structure
        The Toppar data-structure
            
    Side-effects
    ------------
    A DUM residue is appended to each segid and placed at the origin
    }

    ! define topology of DUM residue
    topology
        mass DD 100.00
        residue DUM group
            atom DUM type=DD charge=0.000 end
            atom MAP type=DD charge=0.000 end
        end
    end
    ! define non-bonded parameters
    parameter
        nonbonded DD 0.001 0.001 0.001 0.001 end
    end

    ! place dummy atom of dummy residue in the center of each chain
    evaluate($nchain1 = 0)
    while ($nchain1 < &ncomponents) loop nloopdum
        evaluate($nchain1 = $nchain1 + 1)

        ! create the dummy residue
        segment
            name="TMP1"
            chain 
                sequence "DUM" end
            end
        end

        ! set the residue id of dummy to last residue + 1
        show max(decode(resid)) (segid &Toppar.prot_segid_$nchain1)
        evaluate($maxresid = $result)

        ! change the segid of the dummy residue
        do (segid = &Toppar.prot_segid_$nchain1) (segid TMP1)

        ! set residue id and coordinate of the dummy residue
        do (x = 0) (segid &Toppar.prot_segid_$nchain1 and resn DUM)
        do (y = 0) (segid &Toppar.prot_segid_$nchain1 and resn DUM)
        do (z = 0) (segid &Toppar.prot_segid_$nchain1 and resn DUM)
        do (resid = encode($maxresid + 1)) (segid &Toppar.prot_segid_$nchain1 and resn DUM)

    end loop nloopdum
